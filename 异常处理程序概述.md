
我们知道，操作系统依靠中断与异常进行实时事务的处理。当系统进入中断或者异常时，处理器不会继续运行后面的代码，而是进入异常处理程序。为了达到针对性地处理异常的目的，处理器会首先根据异常的分类，进入特定的异常处理程序——这是通过一个函数入口列表来实现的，我们通常称之为异常处理向量。为了能够让处理器找到并根据这个向量进行跳转，这个向量的格式是预先定义好的，其首地址被存放在一个固定的寄存器 VBAR_ELN 中。在我们的系统中，它看起来是这个样子：

    .macro vector handler
        .align 7
        b \handler
    .endm

    .align 11
    .globl vectors
    vectors:
        vector SYNStub
        vector IRQStub
        vector SYNStub
        vector SYNStub

        vector SYNStub
        vector IRQStub
        vector SYNStub
        vector SYNStub

        vector SYNStub
        vector IRQStub
        vector SYNStub
        vector SYNStub

        vector SYNStub
        vector IRQStub
        vector SYNStub
        vector SYNStub

vectors 就是中断处理向量。代码中的 SYNStub 和 IRQStub 就是异常处理函数了，我们暂且不去管它的内容。从建个就可以看出，这个中断处理向量分为四组。理论上其中每一个处理函数都不应该和其他的相同，但我们的系统尚不完善，目前这样暂时够用了。至于这四组分别对应着什么情况，组内的每一个函数入口又代表着什么类型，格式又是什么，还请参阅 [ARM Cortex-A Series Programmer's Guide for ARMv8-A](http://infocenter.arm.com/help/topic/com.arm.doc.den0024a/DEN0024A_v8_architecture_PG.pdf)。 我们暂时只需要知道 IRQStub 目前是用来处理一般中断的，其他异常都由 SYNStub 处理就够了。

跳转到对应的异常处理函数以后，首先必须进行的是保存异常当时用户程序的状态。一旦异常处理程序进行了异常处理动作，寄存器的值就会改变。如果不提前保存寄存器的状态，返回用户程序后就没有办法继续正确运行了。保存寄存器就是把寄存器的值压入 TIMESTACK 栈内，代码如下所示，这里就不详述了：

    .macro push_time_stack
        str x0, [sp, #-16]!
        ldr x0, =0xffffff0001751000             // TIMESTACKTOP

        stp x29, x30, [x0, #-16]!
        stp x27, x28, [x0, #-16]!
        stp x25, x26, [x0, #-16]!
        stp x23, x24, [x0, #-16]!
        stp x21, x22, [x0, #-16]!
        stp x19, x20, [x0, #-16]!
        stp x17, x18, [x0, #-16]!
        stp x15, x16, [x0, #-16]!
        stp x13, x14, [x0, #-16]!
        stp x11, x12, [x0, #-16]!
        stp x9, x10, [x0, #-16]!
        stp x7, x8, [x0, #-16]!
        stp x5, x6, [x0, #-16]!
        stp x3, x4, [x0, #-16]!
        stp x1, x2, [x0, #-16]!

        mov x21, x0
        ldr x0, [sp], #16
        mrs x20, sp_el0
        stp x20, x0, [x21, #-16]!

        mrs x19, esr_el1
        mrs x20, elr_el1
        stp x20, x19, [x21, #-16]!

        mrs x19, spsr_el1
        str x19, [x21, #-8]!
    .endm

保存寄存器后，中断处理程序就可以开始处理异常了。不过对于种类繁多的异常来说，仅仅在跳入处理程序之前区分的 寥寥几个种类并不能体现异常之间的区别。因此，在进入异常处理程序以后还需要对其做进一步区分。要做到这一点，可以借助于 ESR_ELN 寄存器。这个寄存器保存着异常代码，将其与表格比较，就能够知道异常的具体类别了。例如：

    mrs x19, esr_el1
    lsr x19, x19, #26

    cmp x19, #0x24
    b.eq data_abort_handler

    cmp x19, #0x15
    b.eq svc_handler

    b generic_handler

进入更加具体的异常处理程序后，程序就会根据异常的具体情况进行处理了。当然，在处理的最后，异常处理程序会将寄存器从 TIMESTACK 栈恢复到进入异常之前的状态，并从异常中恢复，这样用户程序就能够开心地继续运行了。
